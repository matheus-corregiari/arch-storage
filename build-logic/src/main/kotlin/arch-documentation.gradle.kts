/**
 * Configures documentation and coverage tooling for publishable modules.
 *
 * The plugin enables Dokka, Kover, Jacoco, and coverage thresholds so CI can generate API reference
 * material and enforce coverage consistently.
 */
import kotlinx.kover.gradle.plugin.dsl.CoverageUnit
import kotlinx.kover.gradle.plugin.dsl.KoverProjectExtension
import org.gradle.internal.extensions.stdlib.capitalized
import org.jetbrains.dokka.gradle.DokkaExtension

plugins {
    id("org.jetbrains.dokka")
    id("org.jetbrains.kotlinx.kover")
    jacoco
}
extensions.configure(JacocoPluginExtension::class) {
    toolVersion = libraries.version("jacoco")
}

extensions.configure(DokkaExtension::class) {
    moduleName.set(project.name.capitalized())
    moduleVersion.set(project.versionName)
    basePublicationsDirectory.set(file("$rootDir/docs/api/${project.name}"))
    dokkaPublications.getByName("html").outputDirectory = basePublicationsDirectory
    dokkaPublications.configureEach { failOnWarning.set(true) }

    dokkaSourceSets.configureEach {
        reportUndocumented.set(true)
        skipDeprecated.set(true)
        skipEmptyPackages.set(true)
        jdkVersion.set(projectJavaVersionCode)
        enableAndroidDocumentationLink.set(true)
        enableJdkDocumentationLink.set(true)
        enableKotlinStdLibDocumentationLink.set(true)
    }
}

extensions.configure(KoverProjectExtension::class) {
    reports {
        total {
            verify {
                rule("Minimum line coverage") {
                    minBound(providers.gradleProperty("ci.coverage.lines").get().toInt(), CoverageUnit.LINE)
                }
                rule("Minimum instruction coverage") {
                    minBound(providers.gradleProperty("ci.coverage.instructions").get().toInt(), CoverageUnit.INSTRUCTION)
                }
                rule("Minimum branch coverage") {
                    minBound(providers.gradleProperty("ci.coverage.branches").get().toInt(), CoverageUnit.BRANCH)
                }
            }
        }
    }
}
